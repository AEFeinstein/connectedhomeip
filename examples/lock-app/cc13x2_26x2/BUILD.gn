# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/defaults.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/ti_simplelink_sdk.gni")
import("${ti_simplelink_sdk_build_root}/ti_simplelink_executable.gni")
import("${ti_simplelink_sdk_build_root}/ti_simplelink_sdk.gni")

assert(current_os == "freertos")

project_dir = "${chip_root}/examples/lock-app/cc13x2_26x2"

ti_simplelink_sdk("sdk") {
  include_dirs = [
    "${project_dir}",
    "${project_dir}/main",
    "${project_dir}/main/include",
    "${chip_root}/src/platform/cc13x2_26x2",
    "${project_dir}/main/include/",
    "${project_dir}/main/",
    "${chip_root}/third_party/openthread/repo/src/core",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd",
    "${ti_simplelink_sdk_root}/source/ti/thread/rtos/${ti_simplelink_board}/thread/libmbedcrypto/config",
  ]

  sources = [
    "${project_dir}/main/include/CHIPProjectConfig.h",
    "${project_dir}/main/include/FreeRTOSConfig.h",
  ]

  defines = []
  if (is_debug) {
    defines += [ "BUILD_RELEASE=0" ]
  } else {
    defines += [ "BUILD_RELEASE=1" ]
  }
}

source_set("openthread_platform") {
  configs -= [ "//build/config/compiler:std_default" ]
  configs += [ ":sdk_posix_config" ]
  defines = [ "NVOCMP_POSIX_MUTEX" ]
  cflags = [
    "-Wno-int-conversion",
    "-Wno-address-of-packed-member",
  ]
  include_dirs = [
    "${chip_root}/third_party/openthread/repo/examples/platforms",
    "${chip_root}/third_party/openthread/repo/src/core",
    "${ti_simplelink_sdk_root}/source/ti/devices/cc13x2_cc26x2",
  ]
  public_deps = [
    ":sdk",
    ":sysconfig",
    "${chip_root}/third_party/openthread/repo:libopenthread-cli-mtd",
    "${chip_root}/third_party/openthread/repo:libopenthread-mtd",
  ]
  sources = [
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/crypto/aes_alt.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/crypto/ecjpake_alt.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/crypto/sha256_alt.c",

    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/alarm.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/alarm_micro.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/diag.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/entropy.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/misc.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/radio.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/settings.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/system.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/uart.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/nv/crc.c",
    "${ti_simplelink_sdk_root}/examples/rtos/${ti_simplelink_board}/thread/cli_mtd/platform/nv/nvocmp.c",
  ]
}

ti_sysconfig("sysconfig") {
  sources = [ "${project_dir}/chip.syscfg" ]
  outputs = [
    "ti_devices_config.c",
    "ti_radio_config.c",
    "ti_radio_config.h",
    "ti_drivers_config.c",
    "ti_drivers_config.h",

    # disabled until upstream generation is aligned
    #"tiop_config.h",
    #"tiop_config.c",

    # not traditional source files
    #"ti_utils_build_linker.cmd.genlibs",
    #"syscfg_c.rov.xs",
    #"ti_utils_runtime_model.gv",
    #"ti_utils_runtime_Makefile",
  ]
}

ti_simplelink_executable("lock_app") {
  include_dirs = [
    "${project_dir}",
    "${project_dir}/main",
    "${project_dir}/main/include",
    "${chip_root}/src/include",
    "${chip_root}/src/lib",
    "${chip_root}/src",
    "${chip_root}/src/app/util",
    "${chip_root}/src/system",
    "${chip_root}/examples/common/chip-app-server/include",
    "${chip_root}/examples/lock-app/lock-common",
    "${chip_root}/third_party/openthread/repo/examples/platforms",
  ]
  defines = []
  output_name = "chip-cc13x2_26x2-lock-example.out"

  public_deps = [
    ":sdk",
    ":sysconfig",
    ":openthread_platform",
    "${chip_root}/examples/common/chip-app-server:chip-app-server",
    "${chip_root}/examples/lock-app/lock-common",
    "${chip_root}/src/lib",
    "${chip_root}/src/setup_payload",
    "${chip_root}/third_party/openthread/repo:libopenthread-cli-mtd",
    "${chip_root}/third_party/openthread/repo:libopenthread-mtd",
  ]

  sources = [
    "${project_dir}/main/AppTask.cpp",
    "${project_dir}/main/BoltLockManager.cpp",
    "${project_dir}/main/ZclCallbacks.cpp",
    "${project_dir}/main/main.cpp",

    # platform drivers here because they need sysconfig headers
    "${chip_root}/src/platform/cc13x2_26x2/Logging.cpp",
  ]

  output_dir = root_out_dir

  ldscript = "${ti_simplelink_sdk_root}/source/ti/boards/cc13x2_cc26x2/cc13x2_cc26x2_freertos.lds"

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir) ]
}

group("cc13x2_26x2") {
  deps = [ ":lock_app" ]
}

group("default") {
  deps = [ ":cc13x2_26x2" ]
}
